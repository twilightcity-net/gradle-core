buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.townsfolk:gradle-release:1.2'
	}
}


apply plugin: 'release'
apply plugin: "org.dreamscale.core-oss"
apply plugin: "org.dreamscale.integration-test"

repositories {
	jcenter()
}

dependencies {
	compile gradleApi()
	compile "net.jokubasdargis.buildtimer:gradle-plugin:0.1.1"
	compile "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0"
	compile "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"

	mainTestCompile 'commons-io:commons-io:2.4'
	mainTestCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
		exclude group: 'org.codehaus.groovy'
	}
	mainTestCompile 'cglib:cglib-nodep:2.2.2'
	mainTestCompile 'org.objenesis:objenesis:1.3'
}

check.dependsOn integrationTest

license {
	ext.name = "DreamScale, Inc"
	skipExistingHeaders true
}

[licenseMain, licenseFormatMain]*.exclude "licenses/*"
// exclude the test kit source which was copied from a different project (https://github.com/alkemist/gradle-test-kit)
// and has it's own license
[licenseMainTest, licenseFormatMainTest]*.exclude "org/gradle/testkit/**"


publishing_ext {
	publication("mainTest")
	publishJavadoc true
}


ext["organization.repo.url"] = "https://github.com/dreamscale-io"

bintray {
	publications = ['gradleCore', 'gradleCoreTest']
	pkg {
		repo = 'maven-public'
		userOrg = 'dreamscale'
		licenses = ['Apache-2.0']
		labels = ['gradle']
		version {
			vcsTag = project.version
			attributes = ['gradle-plugin': 'org.dreamscale.core-oss:org.dreamscale:gradle-core']
		}
	}
}
